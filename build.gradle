plugins {
    id 'groovy'
    id 'java'
    id 'java-gradle-plugin'
    id 'maven-publish'
    id "com.gradle.plugin-publish" version "0.16.0"
    id 'signing'
}

group 'com.umutata'
version '1.0'

repositories {
    mavenCentral()
}

dependencies {
    compile gradleApi()
    testImplementation 'org.spockframework:spock-core:2.0-groovy-2.5'
    implementation 'com.google.auth:google-auth-library-oauth2-http:1.1.0'
    implementation 'com.google.apis:google-api-services-androidpublisher:v3-rev142-1.25.0'
    implementation 'com.google.guava:guava:30.1.1-jre'
    implementation 'org.apache.httpcomponents:httpmime:4.5.13'
}

test {
    useJUnitPlatform()
}

gradlePlugin {
    plugins {
        storePublisher {
            id = 'com.umutata.StorePublisherPlugin'
            implementationClass = 'com.umutata.StorePublisherPlugin'
        }
    }
}

java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'storepublisher'
            from components.java
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom {
                name = 'Store Publisher'
                description = 'Google Play And Huawei App Gallery File Uploader'
                url = 'https://github.com/Makhosh/storepublisher'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'umutata'
                        name = 'Umut Atacan'
                        email = 'umutatacan@gmail.com'
                    }
                }
            }

        }
    }
    repositories {
        maven {
            def releasesRepoUrl = layout.buildDirectory.dir('repos/releases')
            def snapshotsRepoUrl = layout.buildDirectory.dir('repos/snapshots')
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}


