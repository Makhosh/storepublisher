plugins {
    id 'java-gradle-plugin'
    id 'maven-publish'
    id 'signing'
}

group 'io.github.makhosh'
version '1.0'

repositories {
    mavenCentral()
}

dependencies {
    testImplementation gradleTestKit()
    testImplementation('org.junit.jupiter:junit-jupiter-api:5.8.1')
    testRuntimeOnly('org.junit.jupiter:junit-jupiter-engine:5.8.1')
    implementation 'com.google.auth:google-auth-library-oauth2-http:1.1.0'
    implementation 'com.google.apis:google-api-services-androidpublisher:v3-rev142-1.25.0'
    implementation 'com.google.guava:guava:30.1.1-jre'
    implementation 'org.apache.httpcomponents:httpmime:4.5.13'
}

test {
    useJUnitPlatform()
}

gradlePlugin {
    plugins {
        storePublisher {
            id = 'io.github.makhosh.storepublisher'
            implementationClass = 'com.umutata.StorePublisherPlugin'
        }
    }
}

java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = 'io.github.makhosh'
            artifactId = 'storepublisher'
            version = '1.0'
            from components.java
            pom {
                name = 'Store Publisher'
                description = 'Google Play And Huawei App Gallery File Uploader'
                url = 'https://github.com/Makhosh/storepublisher'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'https://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'Makhosh'
                        name = 'Umut Atacan'
                        email = 'umutatacan@gmail.com'
                    }
                }
                scm {
                    url = 'https://github.com/Makhosh/storepublisher'
                    connection = 'scm:git:git://github.com/Makhosh/storepublisher.git'
                    developerConnection = 'scm:git:ssh://git@github.com:Makhosh/storepublisher.git'
                }
            }

        }
    }
    repositories {
        maven {
            url "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2"
            credentials {
                username sonatypeUsername
                password sonatypePassword
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}


